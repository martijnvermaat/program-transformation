module test-java-propconst
imports
  java-propconst

strategies

  main-test-java-propconst =
    option-wrap(general-options,
      test-suite(!"WK10 assignment",
        observables-wrap(
          simple-tests
        )
      )
    )

strategies

  simple-tests = id
  ; test-compilation-unit(|
      "Evaluation to simple constant value"
    , "class Foo { public void foo() { int x = 3; result : x * x;} }"
    , ExprStm(Lit(Deci("9")))
    )
  ; test-compilation-unit(
     not(oncetd(?ExprName(Id("x"))))
    | "Evaluation to simple constant value"
    , "class Foo { public void foo() { int x = 3; int y; result : x * y;} }"
    )
  ; test-compilation-unit(
     oncetd(?ExprName(Id("y")))
    | "Evaluation to simple constant value"
    , "class Foo { public void foo() { int x = 3; int y; result : x * y;} }"
    )

/**
 * Testing utils
 */
strategies

  test-compilation-unit(check |msg, src) =
    apply-and-check(!msg
    , propconst-compilation-unit
      ; get-test-result             
    , <process-input> src   
    , check
    )
    
  test-compilation-unit(|msg, src, result) =
    apply-test(!msg
    , propconst-compilation-unit
      ; get-test-result
    , <process-input> src   
    , !result
    )

  get-test-result =
    oncetd(?Labeled(Id("result"), stm))
    ; !stm

  process-input =
    print-to
    ; ![<id>]
    ; parse-java
    ; map(define-compilation-unit)
    ; dryad-reclassify
    ; map(dryad-type-checker) 
    ; last
    ; get-ast
