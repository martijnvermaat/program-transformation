module test-java-propconst
imports
  java-propconst

strategies

  main-test-java-propconst =
    option-wrap(general-options,
      test-suite(!"WK10 assignment",
        observables-wrap(
          simple-tests
        ; field-tests
        ; control-flow-tests
        )
      )
    )

strategies

  simple-tests = id
  ; test-compilation-unit(|
      "Evaluation to simple constant value"
    , "class Foo { public void foo() { int x = 3; result : x * x; } }"
    , ExprStm(Lit(Deci("9")))
    )
  ; test-compilation-unit(
     not(oncetd(?ExprName(Id("x"))))
    | "Evaluation to simple constant value"
    , "class Foo { public void foo() { int x = 3; int y; result : x * y; } }"
    )
  ; test-compilation-unit(
     oncetd(?ExprName(Id("y")))
    | "Evaluation to simple constant value"
    , "class Foo { public void foo() { int x = 3; int y; result : x * y; } }"
    )

strategies

  field-tests = id
  ; test-compilation-unit(|
      "Evaluation of field to simple constant value"
    , "class Foo { int x; public void foo() { Foo.x = 3; result : Foo.x * Foo.x; } }"
    , ExprStm(Lit(Deci("9")))
    )
  ; test-compilation-unit(|
      "Evaluation of field to simple constant value"
    , "class Foo { int x; int m; public void foo() { Foo.x = 3; Foo.x = Foo.m; result : Foo.x; } }"
    , ExprStm(Field(TypeName(PackageName([]),Id("Foo")),Id("x")))
    )
  ; test-compilation-unit(
     not(oncetd(?Field(TypeName(PackageName([]),Id("Foo")),Id("x"))))
    | "Evaluation of field to simple constant value"
    , "class Foo { int x; public void foo() { Foo.x = 3; int y; result : Foo.x * y; } }"
    )
  ; test-compilation-unit(
     oncetd(?Field(TypeName(PackageName([]),Id("Foo")),Id("x")))
    | "Evaluation of field to simple constant value"
    , "class Foo { int x; public void foo() { int y; result : Foo.x * y; } }"
    )
  ; test-compilation-unit(|
      "Evaluation of field after method call"
    , "class Foo { int x; public void foo() { Foo.x = 2; result : Foo.x; } }"
    , ExprStm(Lit(Deci("2")))
    )
  ; test-compilation-unit(|
      "Evaluation of field after method call"
    , "class Foo { int x; public void foo() { Foo.x = 2; System.out.println(\"hoi\"); result : Foo.x; } }"
    , ExprStm(Field(TypeName(PackageName([]),Id("Foo")),Id("x")))
    )
  ; test-compilation-unit(|
      "Evaluation of field after method call"
    , "class Foo { int x; public void foo() { int y = 1; Foo.x = 2; System.out.println(\"hoi\"); result : y; } }"
    , ExprStm(Lit(Deci("1")))
    )

strategies

  control-flow-tests = id
  ; test-compilation-unit(|
      "Simple if-then-else statement"
    , "class Foo { boolean m; public void foo() { int x = 3, y = 2; if (Foo.m) { x = 1; } else { x = 1; y = x; } result : x; } }"
    , ExprStm(Lit(Deci("1")))
    )
  ; test-compilation-unit(|
      "Simple if-then-else statement"
    , "class Foo { boolean m; public void foo() { Foo.m = true; int x = 3, y = 2; if (Foo.m) { x = 1; } else { x = 1; y = x; } result : x; } }"
    , ExprStm(Lit(Deci("1")))
    )
  ; test-compilation-unit(
     not(oncetd(?If(_, _, _)))
    | "Simple if-then-else statement (evaluated)"
    , "class Foo { boolean m; public void foo() { Foo.m = true; int x = 3, y = 2; result : if (Foo.m) { x = 1; } else { x = 1; y = x; } x; } }"
    )
  ; test-compilation-unit(|
      "Simple if-then-else statement"
    , "class Foo { boolean m; public void foo() { int x = 3, y = 2; if (Foo.m) { x = 1; } else { x = 1; y = x; } result : y; } }"
    , ExprStm(ExprName(Id("y")))
    )
  ; test-compilation-unit(|
      "Simple if-then-else statement (evaluated)"
    , "class Foo { boolean m; public void foo() { Foo.m = false; int x = 3, y = 2; if (Foo.m) { x = 1; } else { x = 1; y = x + 4; } result: y; } }"
    , ExprStm(Lit(Deci("5")))
    )
  ; test-compilation-unit(|
      "Simple while statement"
    , "class Foo { public void foo() { int x = 3; while(x > y) { x = 3; } result : x; } }"
    , ExprStm(Lit(Deci("3")))
    )
  ; test-compilation-unit(|
      "Simple while statement"
    , "class Foo { public void foo() { while(x > y) { x = 3; } result : x; } }"
    , ExprStm(Field(Id("x")))
    )
  ; test-compilation-unit(|
      "Simple while statement"
    , "class Foo { public void foo() { int x = 3; while(x > y) { x = 2; } result : x; } }"
    , ExprStm(ExprName(Id("x")))
    )
  ; test-compilation-unit(|
      "Simple while statement with constant false condition"
    , "class Foo { public void foo() { int x; result: while(false) { x = 2; } x; } }"
    , Block([])
    )
  ; test-compilation-unit(|
      "Simple while statement with constant false condition"
    , "class Foo { public void foo() { int x = 3; boolean m = false; result: while(m) { x = 2; } x; } }"
    , Block([])
    )
  ; test-compilation-unit(|
      "Simple while statement with constant false condition"
    , "class Foo { public void foo() { int x = 3; boolean m = false; while(m) { x = 2; } result : x; } }"
    , ExprStm(Lit(Deci("3")))
    )

/**
 * Testing utils
 */
strategies

  test-compilation-unit(check |msg, src) =
    apply-and-check(!msg
    , propconst-compilation-unit
      ; get-test-result             
    , <process-input> src   
    , check
    )
    
  test-compilation-unit(|msg, src, result) =
    apply-test(!msg
    , propconst-compilation-unit
      ; get-test-result
      ; strip-annos
    , <process-input> src   
    , !result
    )

  get-test-result =
    oncetd(?Labeled(Id("result"), stm))
    ; !stm

  process-input =
    print-to
    ; ![<id>]
    ; parse-java
    ; map(define-compilation-unit)
    ; dryad-reclassify
    ; map(dryad-type-checker) 
    ; last
    ; get-ast
