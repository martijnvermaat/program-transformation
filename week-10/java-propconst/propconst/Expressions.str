module propconst/Expressions
imports
  propconst/Rules

strategies

  /**
   * Propagate constants in expressions bottomup.
   */
  propconst-exp =
    bottomup-exp(
      try(
           PropConst
        <+ EvalBinOp
        <+ EvalCond
      )
    )

  /**
   * Bottomup traversal over expressions.
   */
  bottomup-exp(s) =
       propconst-var-assignment
    <+ propconst-field-assignment
    <+ propconst-method-invocation
    <+ all(bottomup-exp(s))
       ; s

  /**
   * Propagate constants in assignment to local variable.
   */
  propconst-var-assignment =
    Assign(ExprName(Id(?var)), propconst-exp => e)
    ; if <is-value> e then
        rules( PropConst.var : ExprName(Id(var)) -> e )
      else
        rules( PropConst.var :- ExprName(Id(var)) )
      end

  /**
   * Propagate constants in assignment to field. Field values are dependent on
   * method invocations.
   * Only support fully qualified field names.
   */
  propconst-field-assignment =
    Assign(?field@Field(_, _), propconst-exp => e)
    ; if <is-value> e then
        rules( PropConst+field : field -> e
               depends on [(field, "inv")] )
      else
        rules( PropConst+field :- field )
      end

  /**
   * Throw away PropConst rules for all fields at method invocation.
   */
  propconst-method-invocation =
    ?Invoke(_, _)
    ; undefine-PropConst(|"inv")

  /**
   * Succeed if current term is a fully evaluated expression.
   */
  is-value =
    ?Lit(_)
