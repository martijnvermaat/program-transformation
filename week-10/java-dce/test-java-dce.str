module test-java-dce
imports
  java-dce

strategies

  main-test-java-dce =
    option-wrap(general-options,
      test-suite(!"WK10 assignment",
        observables-wrap(
            simple-tests
          ; complex-tests
        )
      )
    )

strategies

  simple-tests = id
  ; test-compilation-unit(|
      "Evaluation to simple constant value"
    , "class Foo { public void foo() { int x = 3; result : x * x; } }"
    , ExprStm(Lit(Deci("9")))
    )
  ; test-full-compilation-unit(|
      "Test dce"
    , "class Foo { void foo() { int x = 0; int y = x * x; return x; } }"
    , "class Foo { void foo() { int x = 0; return x; } }"
    )

strategies

  complex-tests = id
  ; test-full-compilation-unit(|
      "Test from TIL example"
    , "class Hoi {
            void foo() {
                int x;
                int y;
                int z;
                int a;
                int b;
                x = 1;
                y = 2;
                x = 3;
                a = 7;
                y = 3 + z;
                if (y > 0) {
                    z = 8;
                    x = 3;
                } else {
                    x = 3;
                    z = 7 + z;
                }
                b = 7 + z;
                z = 10;
                System.out.println(b + 10);
            }
        }
      "
    , "class Hoi {
          void foo() {
            int y;
            int z;
            int b;
            y = 3 + z;
            if(y > 0)
              z = 8;
            else
              z = 7 + z;
            b = 7 + z;
            java.lang.System.out.println(b + 10);
          }
        }
      "
    )

/**
 * Testing utils
 */
strategies

  test-compilation-unit(check |msg, src) =
    apply-and-check(!msg
    , dce-compilation-unit
      ; get-test-result             
    , <process-input> src   
    , check
    )
    
  test-compilation-unit(|msg, src, result) =
    apply-test(!msg
    , dce-compilation-unit
      ; get-test-result
      ; strip-annos
    , <process-input> src   
    , !result
    )

  test-full-compilation-unit(|msg, src, result) =
    apply-test(!msg
    , dce-compilation-unit; strip-annos
    , <process-input> src
    , <process-input; strip-annos> result
    )

  get-test-result =
    oncetd(?Labeled(Id("result"), stm))
    ; !stm

  process-input =
    print-to
    ; ![<id>]
    ; parse-java
    ; map(define-compilation-unit)
    ; dryad-reclassify
    ; map(dryad-type-checker) 
    ; last
    ; get-ast
