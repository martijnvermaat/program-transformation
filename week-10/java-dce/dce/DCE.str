module dce/DCE
imports
  libdryad
  dce/Rules

strategies

  /**
   * Eliminate dead code in method body.
   * Don't use dce-stm here, because that may result in an Empty statement.
   * todo: return stm
   * todo: fields
   */
  dce-method-body =
    {| VarNeeded, VarUsed : dce-block-stm |}

  dce-stm =
       dce-empty-stm
    <+ dce-block-stm
       ; try(ElimBlock)
    <+ dce-expr-stm
       ; try(ElimBlock)
    <+ dce-local-var-dec-stm
    <+ dce-if-then-else-stm
       ; repeat(ElimIf)
       ; try(dce-expr-stm)
    <+ dce-while-stm
    <+ dce-labeled-stm

  dce-empty-stm =
    ?Empty

  dce-block-stm =
    Block(
        reverse-map(dce-stm)
      ; filter(not(?Empty))
    )

  dce-expr-stm =
    ?ExprStm( <id> )
    ; (dce-assign-expr <+ dce-simple-expr)
    ; !Block( <map(!ExprStm( <id> ) )> )

  /* assumption: assignment is always toplevel expression */
  /* return list of expressions */
  dce-assign-expr =
    ?Assign(ExprName(Id(var)), _)
    ; if ElimAssign
      then dce-simple-expr
      else ![ <Assign(id, declare-vars-needed)> ]
           ; rules( VarNeeded.var :- var )
      end

  /* return list of expressions */
  dce-simple-expr =
    collect(?Invoke(_, _))
    ; reverse-map(declare-vars-needed)

  /**
   * Local variable declaration.
   */
  dce-local-var-dec-stm =
    ?LocalVarDecStm(_)
    ; (
         LocalVarDecStm(LocalVarDec(id, id, 
             reverse-map(dce-var-dec)
           ; filter(is-used-var-dec)
           ; not(?[])
         ))
         <+ !Empty        /* todo: refactor */
      )

  /**
   * Simple declaration.
   * todo: refactor
   */
  dce-var-dec =
    ?VarDec(_)

  /**
   * Declaration with assignment.
   */
  dce-var-dec =
    VarDec(Id(?var), declare-vars-needed)
    /* In Java, variable can never be used before declaration */
    /* ; rules( VarNeeded.var :- var ) */

  is-used-var-dec =
    ?VarDec(Id(var), expr)
    ; (where( <VarUsed> var )
       <+ where( sometd(?Invoke(_, _)) ))

  is-used-var-dec =
    ?VarDec(Id(var))
    ; where( <VarUsed> var )

  /**
   * If-then-else statement.
   * todo: simple if-then statement
   */
  dce-if-then-else-stm =
    (If(id, dce-stm, id) \VarNeeded,VarUsed/ If(id, id, dce-stm))
    ; If(dce-expr, id, id)

  /**
   * While-statement.
   */
  dce-while-stm =
    (\VarNeeded,VarUsed/* /**/ While(dce-expr, dce-stm))

  dce-labeled-stm =
    Labeled(id, dce-stm)

  declare-vars-needed =
    alltd({var :
      ?ExprName(Id(var))
      ; rules(
          VarNeeded+var : var
          VarUsed+var   : var
        )
    })

  /* return expression */
  /* todo: refactor */
  dce-expr =
       ?Assign(ExprName(Id(var)), _)
       ; if ElimAssign
         then declare-vars-needed
         else Assign(id, declare-vars-needed)
              ; rules( VarNeeded-var :- var )
         end
    <+ declare-vars-needed
