module assimilate-java/Statements
imports
  assimilate-java/Util
  assimilate-java/Expressions

rules

  /**
   * Block statement.
   */
  Assimilate(s) :
    Block(stm*) -> <mapconcat(s)> stm*

  /**
   * Empty statement.
   */
  Assimilate(s) :
    Empty -> []

  /**
   * Assimilate expression statement.
   *
   * The value of the expression might stay on the stack unnoticed. Problem is
   * that a store operation implicitly does a pop, so it depends if the
   * expression is an assignment or not.
   * Easiest solution is to do a dup before every assignment and pop after all
   * expression statements. This is not in the spirit of store, but it makes
   * assignments deeper inside expressions work.
   * The Java compiler is a bit smarter about this. It inserts a dup if it
   * sees that the assignment is deeper inside an expression.
   */
  Assimilate(s) =
    ?ExprStm(<id>)
    ; <concat> [ <s>
               , bstm* |[ thisCode.emitPop(1); ]| ]

  /**
   * Assimilate local variable declaration.
   */
  Assimilate(s) =
    ?LocalVarDecStm(LocalVarDec(_, type, <id>))
    ; mapconcat( assimilate-local-var-dec(|type) )

  /**
   * Assimilate return statement.
   */
  Assimilate(s) :
    bstm |[ return e; ]| -> bstm* |[ ~*< <s> e >
                                     thisCode.emitReturn(); ]|

  Assimilate(s) :
    bstm |[ return; ]| -> bstm* |[ thisCode.emitReturn(); ]|

  /**
   * Assimilate do-while statement.
   */
  Assimilate(s) :
    DoWhile(body, condition) ->
    bstm* |[ Label ~label = new Label(thisCode);
             ~<!label>.define(thisCode);
             ~*code
             thisCode.emitPushInt(0);
             thisCode.emitGotoIfGt( ~<!ExprName(label)> ); ]|
    where   !Id(< <newname> "start"> ) => label
          ; <concat> [ <s> body
                     , <s> condition ] => code

  /**
   * Assimilate simple local variable declaration.
   */
  assimilate-local-var-dec(|type) :
    VarDec(Id(name)) ->
    bstm* |[ Variable ~var = thisCode.addLocal( ~vartype, ~varstring ); ]|
    where   !Id( <declare-local-var(|name, type)> ) => var
          ; <assimilate-type> type                  => vartype
          ; !Lit(String([Chars(name)]))             => varstring

  /**
   * Assimilate local variable declaration with assignment.
   * todo: Local variable declaration with assignment.
   */
  assimilate-local-var-dec(|type) :
    VarDec(Id(name), exp) -> []
    where   debug(!"Not implemented: ")
          ; fail
