module assimilate-java/Expressions
imports
  Java-EBlock

rules

  /**
   * Assimilate decimal constant.
   * todo: Is this correct wrt typing?
   */
  Assimilate(s) :
    e |[ i ]| -> bstm* |[ thisCode.emitPushInt(i); ]|

  /**
   * Assimilate local variable.
   */
  Assimilate(s) :
    e |[ x ]| -> bstm* |[ thisCode.emitLoad( ~var ); ]|
    where !ExprName(Id( <LocalVar> x )) => var

  /**
   * Assimilate binary operator.
   * todo: Use PrimType argument, eg Type.int_type for emitAdd method. This is
   * actually quite nasty, because Java expects us to do implicit casts, for
   * example 5 + 4l is a long-add with a conversion from int to long on the
   * first number. We won't do things like this.
   */
  Assimilate(s) :
    op#([e1, e2]) -> bstm* |[ ~*operands
                              thisCode.~method(); ]|
    where   <assimilate-operator> op => method
          ; <concat> [ <s> e1
                     , <s> e2 ] => operands


rules

  assimilate-operator : "Plus"  -> Id("emitAdd")
  assimilate-operator : "Mul"   -> Id("emitMul")
  assimilate-operator : "Minus" -> Id("emitSub")
  assimilate-operator : "Div"   -> Id("emitDiv")
