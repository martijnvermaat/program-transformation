module assimilate-java/Expressions
imports
  Java-EBlock

rules

  /**
   * Assimilate decimal constant.
   * todo: Is this correct wrt typing?
   */
  Assimilate(s) :
    e |[ i ]| -> bstm* |[ thisCode.emitPushInt(i); ]|

  /**
   * Assimilate local variable.
   */
  Assimilate(s) :
    e |[ x ]| -> bstm* |[ thisCode.emitLoad( ~var ); ]|
    where !ExprName(Id( <LocalVar> x )) => var

  /**
   * Assimilate binary operator.
   */
  Assimilate(s) :
    op#([e1, e2]) -> <conc> (stm1, stm2, emit)
    where   <assimilate-operator> op => method
          ; <s> e1 => stm1
          ; <s> e2 => stm2
          ; !bstm* |[ thisCode.~method(); ]| => emit

rules

  assimilate-operator : "Plus"  -> Id("emitAdd")
  assimilate-operator : "Mul"   -> Id("emitMul")
  assimilate-operator : "Minus" -> Id("emitSub")
  assimilate-operator : "Div"   -> Id("emitDiv")
