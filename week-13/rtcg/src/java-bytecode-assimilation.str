module java-bytecode-assimilation
imports
  libdryad
  Java-15-in-Java-15
  Java-EBlock
  assimilate-java/Classes
  assimilate-java/Statements
  assimilate-java/Expressions

strategies

  main =
    init-observables
    ; xtc-multi-io-wrap(
        observables-wrap(
          map(read-from)
          ; map(define-compilation-unit)
          ; dryad-reclassify
          ; map(get-ast)
          ; alltd(assimilate-in-method)
        )
      )

  init-observables =
    where(
      <set-config> (ObservableClasses(), [
        <xtc-find> "rt.classes",
        "../gnu-bytecode/kawa-1.8.jar"
      ])
    )

rules

  /**
   * Find quoted code fragments in methods.
   */
  assimilate-in-method :
    MethodDec(head1, block1) -> MethodDec(head2, block2)
    where {| LocalVar, ArrayType
           : <alltd(declare-meta-param)> head1 => head2
           ; <alltd(assimilate-quotes)> block1 => block2
           |}

  /**
   * Declare parameters marked as meta-var; use dynamic rule to propagate to
   * uses of variables.
   */
  declare-meta-param :
    Param([], t, MetaVar(Id(x))) -> Param([], t, Id(var))
    where <newname> "var" => var
          ; rules( LocalVar : x -> var )

  /**
   * Assimilate quoted fragments.
   */
  assimilate-quotes =
    assimilate-quoted-block-stms
    <+ assimilate-quoted-method
    <+ assimilate-quoted-class
    <+ assimilate-meta-var

  /**
   * Assimilate a quoted class declaration. The assimilated fragment is an
   * expression that produces a ClassType.
   * Declaration of variables used in assimilation is done in a block, so they
   * can be used more than once.
   */
  assimilate-quoted-class :
    QuoteClassDec(cdec) ->
    expr|[ {|
              ClassType ~className;
              {
                ClassType thisClass;
                Method thisMethod;
                CodeAttr thisCode;
                Variable thisVar;
                ~bstm*:<assimilate-embedding> cdec
                ~className = thisClass;
              }
              | ~className
           |} ]|
    where !Id( <newname> "class" ) => className

  /**
   * The hack in this is that there has to be a ClassType named thisClass
   * visible to quote a method declaration.
   */
  assimilate-quoted-method :
   QuoteMethodDec(mdec) -> bstm |[ {
                                     Method thisMethod;
                                     CodeAttr thisCode;
                                     ~bstm*:<assimilate-embedding> mdec
                                   } ]|

  /**
   * In the same way, there has to be a visible CodeAttr named thisCode.
   */
  assimilate-quoted-block-stms :
   QuoteBlockStms(stms) -> Block( <mapconcat(assimilate-embedding)> stms )

  assimilate-meta-var :
    MetaVar(Id(x)) -> Id(var)
    where <LocalVar> x => var

strategies

  /**
   * Assimilate embedded Java code with possible anti quotations.
   */
  assimilate-embedding =
       assimilate-anti-quote
    <+ /* repeat(Desugar) ; */
       Assimilate(assimilate-embedding)
    <+ debug(!"Cannot assimilate embedded Java: ")
       ; fail

  /**
   * Assimilate in an anti quotation.
   */
  assimilate-anti-quote :
    EscapeFromStm(bstms) -> bstms'
    where <alltd(assimilate-quotes)> bstms => bstms'

  /**
   * Assimilate integer meta expression.
   */
  assimilate-anti-quote :
    JavaInt(e) -> bstm* |[ thisCode.emitPushInt( e ); ]|

  /**
   * Assimilate double meta expression.
   */
  assimilate-anti-quote :
    JavaDouble(e) -> bstm* |[ thisCode.emitPushDouble( e ); ]|
