module typestate/Statements
imports
  typestate/States
  typestate/Expressions

strategies

  /**
   * todo: define nullness of test methods, returns, throws, checks in
   *       conditionals, lazy operators
   * todo: always unlock
   */

  typestate-method-dec =
    {| Typestate :
       MethodDec(typestate-method-head, typestate-method-body)
    |}

  typestate-method-head =
    collect(?Param(_, _, _))
    ; map({ var :
        ?Param(_, type, Id(var))
        ; (where(<ParameterState>var => ts)
           <+ <debug(!"don't know this method parameter: ")>var)
        ; rules ( Typestate : var -> ts )
      })

  typestate-method-body =
    typestate-stm

  typestate-stm =
       typestate-empty-stm
    <+ typestate-block-stm
    <+ typestate-expr-stm
    <+ typestate-local-var-dec-stm
    <+ typestate-if-then-else-stm
    <+ typestate-while-stm
    <+ typestate-labeled-stm
    <+ typestate-return-stm
    <+ debug(!"unsupported statement: ")

  /**
   * Do nothing with empty statement.
   */
  typestate-empty-stm =
    ?Empty

  /**
   * Statements in block.
   */
  typestate-block-stm =
    Block( map(typestate-stm) )

  /**
   * Propagate typestate in expression.
   */
  typestate-expr-stm =
    ExprStm(typestate-exp)

  /**
   * Analyse typestate in local variable declaration.
   */
  typestate-local-var-dec-stm =
    ?LocalVarDecStm(LocalVarDec(_, type, _))
    ; LocalVarDecStm(LocalVarDec(id, id, map(typestate-var-dec(|type))))

  /**
   * Simple declaration.
   */
  typestate-var-dec(|type) =
    VarDec(Id(?var))
    ; where(typestate-declaration-init(|type) => ts)
    ; rules ( Typestate : var -> ts )

  /**
   * Declaration with assignment.
   */
  typestate-var-dec(|type) =
    VarDec(Id(?var), typestate-exp => e)
    ; where(
        <typestate-assignment-rule>(
          <typestate-declaration-init(|type)>,
          <typestate-of>e
        ) => ts
      )
    ; rules (
        Typestate : var -> ts
      )

  /**
   * todo: consider conditional checks for nullness
   */
  typestate-if-then-else-stm =
    If(typestate-exp, id, id)
    ; merge-Typestate(
        If(id, typestate-stm, id),
        If(id, id, typestate-stm)
      )

  /**
   * todo: looping body does matter for null annotations
   */
  typestate-while-stm =
    While(typestate-exp, id)
    ; merge-Typestate(id, While(typestate-exp, typestate-stm))

  /**
   * Labeled statement.
   */
  typestate-labeled-stm =
    Labeled(id, typestate-stm)

  /**
   * Return statement.
   */
  typestate-return-stm =
    Return(typestate-exp)
