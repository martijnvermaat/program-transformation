module typestate/Statements
imports
  typestate/States
  typestate/Expressions

strategies

  typestate-method-body =
    {| Typestate : typestate-stat |}

  typestate-stat =
       typestate-empty-stm
    <+ typestate-block-stm
    <+ typestate-expr-stm
    <+ typestate-local-var-dec-stm
/*
    <+ typestate-if-then-else-stm
    <+ typestate-while-stm
    <+ typestate-labeled-stm
*/
    <+ debug(!"unsupported statement: ")

  /**
   * Do nothing with empty statement.
   */
  typestate-empty-stm =
    ?Empty

  /**
   * Statements in block.
   */
  typestate-block-stm =
    Block( map(typestate-stat) )

  /**
   * Propagate typestate in expression.
   */
  typestate-expr-stm =
    ExprStm(typestate-exp)

  /**
   * Analyse typestate in local variable declaration.
   */
  typestate-local-var-dec-stm =
    LocalVarDecStm(LocalVarDec(id, id, map(typestate-var-dec)))

  /**
   * Simple declaration.
   */
  typestate-var-dec =
    VarDec(Id(?var))
    ; rules ( Typestate : var -> UnInit )

  /**
   * Declaration with assignment.
   */
  typestate-var-dec =
    VarDec(Id(?var), typestate-exp => e)
    ; where(<typestate-of>e => ets)
    ; rules (
        Typestate : var -> UnInit
        Typestate : var -> Init  /* todo: parameterize this to use ets */
      )
